#version 460

layout(local_size_x = 16, local_size_y = 16) in;

layout(set = 0, binding = 0, rgba16) uniform readonly image2D inputTexture;
layout(set = 0, binding = 1, rgba16) uniform writeonly image2D outputTexture;

layout(push_constant) uniform constants {
	bool horizontal;
	float blurStrength;
};

float weight[5] = float[] (0.227027, 0.1945946, 0.1216216, 0.054054, 0.016216);

void main() {
	ivec2 coords = ivec2(gl_GlobalInvocationID.xy);
	ivec2 size = imageSize(inputTexture);
	if (coords.x < size.x && coords.y < size.y) {
		vec3 result = imageLoad(inputTexture, coords).rgb * weight[0];
		for(int i = 1; i < 5; i++) {
			ivec2 offset = horizontal ? ivec2(i, 0) : ivec2(0, i);
			result += imageLoad(inputTexture, coords + offset).rgb * weight[i] * blurStrength;
			result += imageLoad(inputTexture, coords - offset).rgb * weight[i] * blurStrength;
		}
	
		imageStore(outputTexture, coords, vec4(result, 1.0));
	}
}